// PROJECT   :CHUMP_ProgramEEPROMBurner
// PURPOSE   :To burn the program data onto the program EEPROM for CHUMP.
// COURSE    :ICS4U-E
// AUTHOR    :William Xie
// DATE      :2025 12 07
// MCU       :328P
// STATUS    :Working
// REFERENCE :http://darcy.rsgc.on.ca/ACES/TEI4M/4BitComputer/index.html#PROGRAMEEPROM

uint8_t addrPins[5] = {A3, A2, A1, A0, A4}; // A4 = slide switch
uint8_t dataPins[8] = { 5, 6, 7, 8, 9, 10, 11, 12 };
uint8_t OE = 3;
uint8_t WE = 4;

// 32 bytes total: two 16-byte programs
uint8_t programData[32] = {
  // PROGRAM 0 (Feinberg's Example Program) - addresses 0x00–0x0F
  0x82, 0x10, 0x21, 0x62,
  0xA0, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00,
  // PROGRAM 1 (Custom CHUMP Program) - addresses 0x10–0x1F
  0x81, 0x10, 0x62, 0x80, 
  0x50, 0xEA, 0x84, 0x10, 
  0x65, 0xC0, 0x83, 0x10, 
  0x65, 0xC0, 0x00, 0x00
};

void setup() {
  Serial.begin(9600);
  for (uint8_t i = 0; i < 5; i++) {
    pinMode(addrPins[i], OUTPUT);
  }
  for (uint8_t i = 0; i < 8; i++) {
    pinMode(dataPins[i], OUTPUT);
  }
  pinMode(OE, OUTPUT);
  pinMode(WE, OUTPUT);
  digitalWrite(OE, HIGH);
  digitalWrite(WE, HIGH);
  Serial.println("Burning BOTH programs...");
  delay(10);
  burnEEPROM();
  Serial.println("Verifying...");
  if (verifyEEPROM()) {
    Serial.println("Verification OK");
  }
  else {
    Serial.println("Verification FAILED");
  }
}

void burnEEPROM() {
  for (uint8_t addr = 0; addr < 32; addr++) {
    // Set A0–A4
    for (uint8_t i = 0; i < 5; i++) {
      digitalWrite(addrPins[i], (addr >> i) & 0x01);
    }
    // Set data
    uint8_t data = programData[addr];
    for (uint8_t i = 0; i < 8; i++) {
      digitalWrite(dataPins[i], (data >> i) & 0x01);
    }
    digitalWrite(WE, LOW);
    delayMicroseconds(1);
    digitalWrite(WE, HIGH);
    delay(10);
  }
}

boolean verifyEEPROM() {
  boolean isValid = true;
  for (uint8_t i = 0; i < 8; i++) {
    pinMode(dataPins[i], INPUT);
  }
  digitalWrite(OE, LOW);
  digitalWrite(WE, HIGH);
  for (uint8_t addr = 0; addr < 32; addr++) {
    for (uint8_t i = 0; i < 5; i++) {
      digitalWrite(addrPins[i], (addr >> i) & 0x01);
    }
    delayMicroseconds(1);
    uint8_t readByte = 0;
    for (uint8_t i = 0; i < 8; i++) {
      if (digitalRead(dataPins[i])) {
        readByte |= (1 << i);
      }
    }
    Serial.print("Addr ");
    Serial.print(addr);
    Serial.print(": ");
    Serial.print(readByte, HEX);
    if(readByte != programData[addr]){
      Serial.println("  MISMATCH");
      isValid = false;
    }
    else {
      Serial.println("  OK");
    }
  }
  digitalWrite(OE, HIGH);
  return isValid;
}

void loop() {} // Nothing to do
